plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'maven'
}

jar.enabled = false
configurations.runtime.artifacts.removeAll { it.archiveTask.is jar }

group = 'com.minecolonies.rankup'
version = System.getenv()['BUILD_NUMBER'] != null ? System.getenv()['BUILD_NUMBER'] : System.getenv()['TRAVIS_BUILD_NUMBER']
description = 'A simple plugin for increasing player ranks based on time'
mainClassName = 'com.minecolonies.rankup.Rankup'
compileJava.options.encoding = 'UTF-8'
ext.url = 'http://github.com/Minecolonies/rankup'

def qsmlDep = 'uk.co.drnaylor:quickstart-moduleloader:0.8.2'
def spongeapi = '6.0.0'

dependencies {
    compileOnly files('libs/magibridge.jar')

    compile('org.spongepowered:spongeapi:' + spongeapi)
    compile(qsmlDep)
}

repositories {
    jcenter()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name 'drnaylor'
        url 'http://repo.drnaylor.co.uk/artifactory/list/quickstart'
    }
}

shadowJar {
    dependencies {
        include dependency(qsmlDep)
        include dependency('io.github.lukehutch:fast-classpath-scanner')
    }

    relocate 'uk.co.drnaylor', 'com.minecolonies.rankup.relocate.uk.co.drnaylor'
    relocate 'io.github.lukehutch', 'com.minecolonies.rankup.relocate.io.github.lukehutch'

    archiveName = "Rankup-V${version}-API6-plugin.jar"
}


blossom {
    def location = 'src/main/java/com/minecolonies/rankup/Plugininfo.java'

    replaceToken '@name@', project.name, location
    replaceToken '@version@', version, location
    replaceToken '@description@', project.description, location
}


build.dependsOn(shadowJar)